defpackage geom/line-segment :
  import core
  import geom/vec
  import geom/mat
  import geom/box
  import geom/geom
  import geom/bounded

val @doc-linesegment = "LINESEGMENT -- line between two points"

#for (Prim in [Double Double]
      Vec in [Vec2 Vec3]
      LineSegment in [LineSegment2 LineSegment3]
      Box in [Box2 Box3]
      LineSegmentName in ["LineSegment2" "LineSegment3"]) :

  public defstruct LineSegment <: Geom&Bounded&Equalable :
    start: Vec
    end:   Vec

  defmethod equal? (a:LineSegment, b:LineSegment) -> True|False :
    start(a) == start(b) and end(a) == end(b)

  defmethod bounds (l:LineSegment) :
    xyz(union(Box(start(l)), Box(end(l))))
  
  public defn center (l:LineSegment) -> Vec :
    0.5 * (start(l) + end(l))

  public defn vector (l:LineSegment) -> Vec :
    end(l) - start(l)

  public defn magnitude (l:LineSegment) -> Prim :
    magnitude(vector(l))

  public defn shorten (l:LineSegment, amount:Double) -> LineSegment :
    val v = normalize(vector(l))
    val r = 0.5 * (magnitude(l) - amount)
    LineSegment(center(l) - r * v, center(l) + r * v)

  defmethod print (o:OutputStream, l:LineSegment) :
    print(o, "%_(%_ %_)" % [LineSegmentName start(l) end(l)])

public defn closest-point-to (l:LineSegment2, p:Vec2) -> Vec2 :
  val l2 = sqr-magnitude(end(l) - start(l))
  if l2 == 0.0 :
    start(l)
  else :
    val dir = vector(l)
    val t = max(0.0, min(1.0, dot(p - start(l), dir) / l2))
    start(l) + t * dir

public defn distance (l:LineSegment2, xy:Vec2) -> Double :
  distance(closest-point-to(l, xy), xy)

;; public defn distance (l:LineSegment2, xy:Vec2) -> Double :
;;   val [p, q] = [start(l), end(l)]
;;   val pq = q - p
;;   val dxy = xy - p
;;   val d = dot(pq, pq)
;;   val t0 = dot(pq, dxy)
;;   val t1 = t0 / d when d > 0.0f else t0
;;   val t = 0.0f when t1 < 0.0f else (1.0f when t1 > 1.0f else t1)
;;   val ndxy = p + t * pq - xy
;;   dot(ndxy, ndxy)

public defn intersects? (a:LineSegment2, b:LineSegment2) -> True|False :
  val [p, r] = [start(a), end(a) - start(a)]
  val [q, s] = [start(b), end(b) - start(b)]
  val r%s = r % s
  val q-p = q - p
  val q-p%r = q-p % r
  val q-p%s = q-p % s
  if r%s == 0.0f :
    if q-p%r == 0.0f :
      val t0 = dot(q-p, r) / dot(r, r)
      val t1 = t0 + dot(s, r) / dot(r, r)
      val bx = Box1(t0) | Box1(t1)
      lo(bx) >= 0.0 and hi(bx) <= 1.0
    else :
      false
  else :
    val t = (q-p % s) / r%s
    val u = (q-p % r) / r%s
    t >= 0.0 and t <= 1.0 and u >= 0.0 and u <= 1.0 

